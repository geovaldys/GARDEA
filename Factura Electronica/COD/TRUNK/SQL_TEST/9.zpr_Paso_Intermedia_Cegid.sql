USE [CAP_INTER]
GO

/****** Object:  StoredProcedure [dbo].[zpr_Paso_Intermedia_Cegid]    Script Date: 8/28/2024 9:18:33 AM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

 /*
Version: 1.0
Autor: Geovaldys Bobadilla
Fecha de creacion: 29/07/2024
Descripcion: Procedimiento encargado DE LLENAR LA TABLA T_PIECE, T_LIGNE, con informacion recuperada desde BD DE CEGID
Compañia: LDS
*/

CREATE PROCEDURE [dbo].[zpr_Paso_Intermedia_Cegid] @fecha varchar(20)=''
AS
BEGIN
   
   if @fecha=''
   begin
     set @fecha=cast(getdate() as varchar(20))
   end

   /****************** CABECERA ***************
    SECCION PARA LLENADO DE LA CABECERA
	T_PIECE
   ****************************************/
   

   IF OBJECT_ID('tempdb..#T_PIECE') IS NOT NULL
	BEGIN
	   Drop Table #T_PIECE
	END

	--TABLA TEMPORAL DE LA T_PIECE
	CREATE TABLE [dbo].[#T_PIECE](
		[GP_PIECE] [int] NULL,
		[GP_ETABLISSEMENT] [nchar](6) NULL,
		[GP_CAISSE] [nchar](6) NULL,
		[GP_NUMERO] [int] NULL,
		[GP_DATEPIECE] [datetime] NULL,
		[GP_SUBTOTAL] [numeric](19, 4) NULL,
		[GP_TOTALHT] [numeric](19, 4) NULL,
		[GP_TOTALTTC] [numeric](19, 4) NULL,
		[GP_TOTALTAXEDEV1] [numeric](19, 4) NULL,
		[GP_REMISELIBRE3] [numeric](19, 4) NULL,
		[GP_ESTATUS] [nchar](10) NULL,
		[GP_TASAS] [numeric](19, 4) NULL,
		[GP_NATUREPIECE] [nchar](3) NULL,
		[GP_MODEPAIE] [nchar](3) NULL,
		[GP_REFINTERNE] [nchar](40) NULL,
		[GP_TICKET_REF] [int] NULL,
		[GP_MODEPAIE_CODE] [nchar](20) NULL
	) ON [PRIMARY]
	
	--OBTENEMOS LOS RESULTADOS DE LA CONSULTA OBTENIDA DE LA BASE DE DATOS DE CEGID Y LA PASAMOS A LA TEMPORAL
	INSERT INTO #T_PIECE
	SELECT 
		0 GP_PIECE,
		GP_SOUCHE,GP_CAISSE,GP_NUMERO,GP_DATEPIECE,CLIGNE.SUBTOTAL GP_SUBTOTAL,GP_TOTALHT,GP_TOTALTTC,
		ISNULL(CLIGNE.TOTALTAXEDEV1,0) GP_TOTALTAXEDEV1, ISNULL(CREMISE.MONTANTTTC,0) GP_REMISELIBRE3,'1' GP_ESTATUS,0 GP_TASAS,
		GP_NATUREPIECEG,CMODEPAIE.MODEPAIE GPE_MODEPAIE,
		GP_REFINTERNE, GP_NUMERO GP_TICKET_REF, 
		(SELECT TOP 1 MP_RACINEREFINT1 FROM [2CAPTEST].[dbo].[MODEPAIE] WHERE MP_MODEPAIE=CMODEPAIE.MODEPAIE) GP_MODEPAIE_CODE
	FROM [2CAPTEST].[dbo].[PIECE]
	OUTER APPLY
	(
		SELECT TOP 1 GPE_MODEPAIE MODEPAIE  FROM [2CAPTEST].[dbo].[PIEDECHE]
		WHERE GPE_NUMERO=GP_NUMERO AND GPE_SOUCHE=GP_SOUCHE AND GPE_NATUREPIECEG=GP_NATUREPIECEG
		ORDER BY GPE_MONTANTECHE DESC
	)CMODEPAIE
	OUTER APPLY 
	(
		SELECT SUM(MLR_MONTANTHT) MONTANTTTC FROM [2CAPTEST].[dbo].[LIGNEREMISE]
		WHERE MLR_NATUREPIECEG=GP_NATUREPIECEG AND MLR_NUMERO=GP_NUMERO AND MLR_SOUCHE=GP_SOUCHE
	) CREMISE
	OUTER APPLY
	(
		SELECT (SUM(GL_TOTALTTC)-SUM(GL_TOTALHT)) TOTALTAXEDEV1, SUM(GL_PUHT*ABS(GL_QTEFACT)) SUBTOTAL FROM [2CAPTEST].[dbo].[LIGNE]
		WHERE GL_NUMERO=GP_NUMERO AND GL_SOUCHE=GP_SOUCHE AND GP_NATUREPIECEG=GL_NATUREPIECEG
	) CLIGNE
	OUTER APPLY
	(
	    SELECT  COUNT(*) CONTADOR FROM [2CAPTEST].[dbo].[LIGNE]
		WHERE GL_NUMERO=GP_NUMERO AND GL_SOUCHE=GP_SOUCHE AND GP_NATUREPIECEG=GL_NATUREPIECEG
		AND GL_CODEARTICLE IN('APARTADO','DEPOSITO','FONDO')
	)CLIGNE_EXCUDE
	WHERE gp_datecreation= cast(@fecha As date)
	--GP_NATUREPIECEG='FFO' AND GP_NUMERO=2214 AND GP_SOUCHE='5069'
	AND GP_NATUREPIECEG = 'FFO'
	AND GP_TICKETANNULE<> 'X'
	AND GP_TYPECOMPTA<>'TRE'
	AND GP_TOTALTTC<>0
	AND CLIGNE_EXCUDE.CONTADOR=0

	--LLENAMOS LA TABLA T_PIECE EN LA INTERMEDIA
	INSERT INTO T_PIECE
	select 
		TEMP.GP_PIECE,TEMP.GP_ETABLISSEMENT,TEMP.GP_CAISSE,TEMP.GP_NUMERO,
		TEMP.GP_DATEPIECE,TEMP.GP_SUBTOTAL,TEMP.GP_TOTALHT,
		TEMP.GP_TOTALTTC,TEMP.GP_TOTALTAXEDEV1,TEMP.GP_REMISELIBRE3,
		TEMP.GP_ESTATUS,TEMP.GP_TASAS,TEMP.GP_NATUREPIECE,TEMP.GP_MODEPAIE,
		TEMP.GP_REFINTERNE ,TEMP.GP_TICKET_REF, TEMP.GP_MODEPAIE_CODE
	from #T_PIECE TEMP
    LEFT JOIN T_PIECE TP on TP.GP_REFINTERNE=TEMP.GP_REFINTERNE   
    WHERE TP.GP_REFINTERNE IS NULL


	/****************** LINEAS ***************
    SECCION PARA LLENADO DE LA LINEAS
	T_LIGNE
   ****************************************/
   IF OBJECT_ID('tempdb..#T_LIGNE') IS NOT NULL
	BEGIN
	   Drop Table #T_LIGNE
	END

    CREATE TABLE [dbo].[#T_LIGNE](
		[GL_PIECE] [int] NULL,
		[GL_ETABLISSEMENT] [nchar](6) NULL,
		[GL_CAISSE] [nchar](10) NULL,
		[GL_NUMERO] [int] NULL,
		[GL_DATEPIECE] [datetime] NULL,
		[GL_NUMLIGNE] [int] NULL,
		[GL_LIBELLE] [nchar](70) NULL,
		[GL_CODEARTICLE] [nchar](18) NULL,
		[GL_REFARTBARRE] [nchar](18) NULL,
		[GL_QTEFACT] [numeric](19, 4) NULL,
		[GL_PUHT] [numeric](19, 4) NULL,
		[GL_SUBTOTAL] [numeric](19, 4) NULL,
		[GL_REMISELIBRE3] [numeric](19, 4) NULL,
		[GL_PORCENREMISE] [numeric](19, 4) NULL,
		[GL_REGIMETAXE] [nchar](10) NULL,
		[GL_TASA] [numeric](19, 4) NULL,
		[GL_TOTALTAXEDEV1] [numeric](19, 4) NULL,
		[GL_TOTALHT] [numeric](19, 4) NULL,
		[GL_TOTALTTC] [numeric](19, 4) NULL,
		[GL_LIBREART4] [nchar](35) NULL,
		[GL_UNIDAD] [nchar](40) NULL,
		[GL_REFINTERNE] [nchar](40) NULL
	) ON [PRIMARY]

   
   INSERT INTO #T_LIGNE
	SELECT 
		0 GL_PIECE,
		GL_SOUCHE,GL_CAISSE,GL_NUMERO,GL_DATEPIECE,GL_NUMLIGNE,GL_LIBELLE,
		GL_CODEARTICLE,GL_REFARTBARRE,GL_QTEFACT
		,GL_PUHT																--Comentado 21/08/2024 por ajuste, Geovaldys Bobadilla
		--,IIF(GL_QTEFACT=0,GL_TOTALHT,(GL_TOTALHT/ABS(GL_QTEFACT))) GL_PUHT		--Agregado 21/08/2024 por ajuste, Geovaldys Bobadilla
		,GL_TOTALHT GL_SUBTOTAL,
		ISNULL(CREMISE.MONTANTTTC,0) GL_REMISELIBRE3,
		ISNULL((SELECT  
				((SUM(ABS(ISNULL(MLR_MONTANTTTCDEV,0))*100)/GL_PUTTCBASE)/GL_QTEFACT) 
		  FROM [2CAPTEST].[dbo].[LIGNEREMISE]
	 	  WHERE MLR_NATUREPIECEG=GL_NATUREPIECEG AND MLR_NUMERO=GL_NUMERO AND MLR_SOUCHE=GL_SOUCHE AND MLR_NUMLIGNE=GL_NUMLIGNE
		),0) GL_PORCENREMISE
		,GL_REGIMETAXE,  ISNULL(CTAXE.GXT_TAUXVTE,0) GL_TASA,
		GL_TOTALTAXEDEV1,GL_TOTALHT,GL_TOTALTTC,GL_LIBCOMPL--GL_LIBREART4
		,'H87' GL_UNIDAD,GP_REFINTERNE
	FROM [2CAPTEST].[dbo].[LIGNE]
	INNER JOIN [2CAPTEST].[dbo].[PIECE] ON GP_NUMERO=GL_NUMERO AND GP_SOUCHE=GL_SOUCHE AND GP_NATUREPIECEG=GL_NATUREPIECEG
	INNER JOIN [2CAPTEST].[dbo].[ETABLISS] ON ET_ETABLISSEMENT=GP_SOUCHE
	OUTER APPLY 
	(
		SELECT SUM(MLR_MONTANTHT) MONTANTTTC FROM [2CAPTEST].[dbo].[LIGNEREMISE]
		WHERE MLR_NATUREPIECEG=GL_NATUREPIECEG AND MLR_NUMERO=GL_NUMERO AND MLR_SOUCHE=GL_SOUCHE AND MLR_NUMLIGNE=GL_NUMLIGNE
	) CREMISE
	OUTER APPLY 
	(
		SELECT GXT_TAUXVTE FROM [2CAPTEST].[dbo].[TAUXTAXE] 
		WHERE GXT_CODETAUX=GL_FAMILLETAXE1 AND GXT_SUPPRIME='-' AND GXT_CODEMODELETAXE=GL_CODEMODELETAXE  AND (GXT_PAYSTAXE=ET_PAYS OR GXT_REGIONTAXE=GL_REGIONTAXE/*ET_PAYS*/)
	) CTAXE
	OUTER APPLY
	(
	    SELECT  COUNT(*) CONTADOR FROM [2CAPTEST].[dbo].[LIGNE]
		WHERE GL_NUMERO=GP_NUMERO AND GL_SOUCHE=GP_SOUCHE AND GP_NATUREPIECEG=GL_NATUREPIECEG
		AND GL_CODEARTICLE IN('APARTADO','DEPOSITO','FONDO')
	)CLIGNE_EXCUDE
	where gp_datecreation= cast(@fecha as date)
	AND GP_NATUREPIECEG = 'FFO'
	AND GP_TICKETANNULE<> 'X'
	AND GP_TYPECOMPTA<>'TRE'
	AND GP_TOTALTTC<>0
	AND GL_TYPEDIM <> 'GEN'
	AND GL_ARTICLE<>''
	AND CLIGNE_EXCUDE.CONTADOR=0

	--LLENAMOS LA TABLA T_PIECE EN LA INTERMEDIA
	INSERT INTO T_LIGNE
	select 
		TEML.GL_PIECE,
		TEML.GL_ETABLISSEMENT,TEML.GL_CAISSE,TEML.GL_NUMERO,TEML.GL_DATEPIECE,TEML.GL_NUMLIGNE,TEML.GL_LIBELLE,
		TEML.GL_CODEARTICLE,TEML.GL_REFARTBARRE,TEML.GL_QTEFACT,TEML.GL_PUHT,TEML.GL_SUBTOTAL,
		TEML.GL_REMISELIBRE3,TEML.GL_PORCENREMISE,TEML.GL_REGIMETAXE,TEML.GL_TASA,
		TEML.GL_TOTALTAXEDEV1,TEML.GL_TOTALHT,TEML.GL_TOTALTTC,TEML.GL_LIBREART4,TEML.GL_UNIDAD,
		TEML.GL_REFINTERNE
	from #T_LIGNE TEML
    LEFT JOIN T_LIGNE TL on TL.GL_REFINTERNE=TEML.GL_REFINTERNE   
    WHERE TL.GL_REFINTERNE IS NULL

END
GO


